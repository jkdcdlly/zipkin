// Adopted from https://github.com/ratpack/ratpack/blob/master/gradle/dependencies.gradle

ext {
    scalaVersion = '2.11.7'
    scalaInterfaceVersion = '2.11'

    commonVersions = [
            finagle: '6.30.0',
            guava: '16.0.1', // to match finagle
            scrooge: '4.2.0',
            twitterUtil: '6.29.0',
            twitterServer: '1.15.0',
            finatra: '2.1.1',
            jackson: '2.6.3',
            ostrich: '9.13.0',
            griddle: '1.7',
            slf4j: '1.7.12',
            logback: '1.1.3',
            mustache: '0.9.1',
            cassandra: '2.2.4', // to ensure tests match docker
            codahaleMetrics: '3.0.2'
    ]

    // Database drivers
    // The keys are the valid values for the system property dbEngine.
    // The values are the dependency to be used in the build to support the requested storage system.
    //
    // The default value of dbEngine is sqlite-persistent, defined in gradle.properties
    // To override, run the build like this: ./gradlew build -PdbEngine=sqlite-memory
    anormDriverDependencies = [
            "sqlite-memory": "org.xerial:sqlite-jdbc:3.8.11.2",  // sqlite-jdbc4 isn't out, yet
            "sqlite-persistent": "org.xerial:sqlite-jdbc:3.8.11.2",
            // MySQL connector is GPL, even if it has an OSS exception.
            // https://www.mysql.com/about/legal/licensing/foss-exception/
            //
            // MariaDB has a friendlier license, LGPL, which is less scary in audits.
            "mysql": "org.mariadb.jdbc:mariadb-java-client:1.3.0"
    ]
}

subprojects { subproject ->
    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                // Ensures libthrift 0.5.0 isn't accidentally in the classpath
                if (details.requested.group == 'org.apache.thrift') {
                  details.useVersion '0.9.0' // https://github.com/twitter/scrooge/issues/203
                }
                // Don't use mixed versions of SLF4J or Logback
                if (details.requested.group == 'org.slf4j') {
                  details.useVersion commonVersions.slf4j
                }
                if (details.requested.group == 'ch.qos.logback') {
                  details.useVersion commonVersions.logback
                }
                if (details.requested.group == 'com.github.spullara.mustache.java') {
                  details.useVersion commonVersions.mustache
                }
                if (details.requested.group == 'com.fasterxml.jackson.core') {
                    details.useVersion commonVersions.jackson
                }
                if (details.requested.group == 'org.apache.cassandra') {
                    details.useVersion commonVersions.cassandra
                }
                if (details.requested.group == 'com.codahale.metrics') {
                    details.useVersion commonVersions.codahaleMetrics
                }
            }
        }

        // Some dependencies early bound slf4j impl
        exclude module: "slf4j-log4j12"
        exclude module: "slf4j-logback"
     }
}
